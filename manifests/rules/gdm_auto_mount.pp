# @summary
#    Ensure automatic mounting of removable media is disabled
#
# By default GNOME automatically mounts removable media when inserted as a convenience to the user.
#
# Rationale:
# With automounting enabled anyone with physical access could attach a USB drive or disc and have its contents
# available in system even if they lacked permissions to mount it themselves.
#
# Impact:
# The use of portable hard drives is very common for workstation users. If your organization allows the use of
# portable storage or media on workstations and physical access controls to workstations is considered adequate
# there is little value add in turning off automounting.
#
# @param enforce
#    Enforce the rule.
#
# @example
#   class { 'cis_security_hardening::rules::gdm_auto_mount':
#     enforce => true,
#   }
#
# @api private
class cis_security_hardening::rules::gdm_auto_mount (
  Boolean $enforce = false,
) {
  $gnome_gdm = fact('cis_security_hardening.gnome_gdm')
  if  $enforce and $gnome_gdm != undef and $gnome_gdm {
    include dconf
    if ($facts['os']['name'].downcase() == 'debian') and
    ($facts['os']['release']['major'] > '10') {
      dconf::db { 'media-automount-autorun-never':
        db_dir         => "${dconf::db_base_dir}/local.d",
        db_filename    => '00-media-automount',
        locks_filename => '00-media-automount',
        settings       => {
          'org/gnome/desktop/media-handling' => {
            'autorun-never' => 'true',
          },
        },
        locks          => [
          '/org/gnome/desktop/media-handling/autorun-never',
        ],
      }
    } else {
      dconf::db { 'media-automount-automount':
        db_dir         => "${dconf::db_base_dir}/local.d",
        db_filename    => '00-media-automount',
        locks_filename => '00-media-automount',
        settings       => {
          'org/gnome/desktop/media-handling' => {
            'automount'      => 'false',
            'automount-open' => 'false',
          },
        },
        locks          => [
          '/org/gnome/desktop/media-handling/automount',
          '/org/gnome/desktop/media-handling/automount-open',
        ],
      }
    }
  }
}
